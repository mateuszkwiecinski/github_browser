apply plugin: 'app.android'

android {
    defaultConfig {
        applicationId "pl.mateuszkwiecinski.browser"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        def token = findGithubToken()
        buildConfigField "String", "GITHUB_TOKEN", "\"$token\""
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled true
    }
    signingConfigs {
        debug {
            storeFile rootProject.file("keys/debug.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
}

dependencies {
    implementation project(":presentation")
    implementation project(":data")
    implementation project(":domain")

    compileOnly Libs.okHttp
    compileOnly Libs.apolloGraphql
}

String findGithubToken() {
    def env = System.getenv('github_token')
    if (env != null) {
        return env
    }
    if (System.hasProperty('github.token')) {
        return System.getProperty("github.token")
    }
    Properties properties = new Properties()
    properties.load(rootProject.file('local.properties').newDataInputStream())

    return properties.getProperty('github.token')
}
