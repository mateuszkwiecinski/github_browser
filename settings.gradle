import org.gradle.api.initialization.resolve.RepositoriesMode

pluginManagement {
    repositories { // https://github.com/gradle/gradle/issues/20866
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "com.gradle.develocity" version "4.0.1"
}

develocity {
    buildScan {
        termsOfUseUrl = "https://gradle.com/terms-of-service"
        termsOfUseAgree = "yes"

        uploadInBackground = System.getenv("CI") == null
        publishing.onlyIf { System.getenv("CI") != null }
    }
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.name = "github_browser"

includeBuild("gradle/plugins")
include ':app'
include ':presentation'
include ':domain'
include ':data:graphql', ':data:mocked'

enableFeaturePreview("TYPESAFE_PROJECT_ACCESSORS")
